module GPIO_TaskBuilder {
	import from TEST_API_Functions all;
	import from TEST_API_Definitions all;
	import from GPIOPinPort all;
	import from TASK_BUILDER all;
	import from TASK all;
	import from GPIO all;
	import from GPIO {import all};
	import from INTERFACE_STORE all;
	
	type component
	CT_GPIO_ReceiveValueTask extends CT_GPIO_Controller, CT_TASK_Task { }
	
	function f_GPIO_ReceiveValueTask (CT_GPIO_Device sourceDevice, GPIO_PIN_VALUE expectedValue)
	runs on CT_GPIO_ReceiveValueTask {
		alt {
			[] p_taskPort.receive(EV_TASK_INIT) {
			  	connect(self:device, sourceDevice:controller);
			  	f_TEST_API_Subject_setverdict(pass, "Task initialised!");
			  	p_taskPort.send(EV_TASK_INITIALISED);
			}
		}
		
		alt {
		  	[] p_taskPort.receive(EV_TASK_START) {
		  	  	
		  	  	var GPIO_PIN_VALUE receivedValue := f_GPIO_Controller_receiveValue();
		  	  	if (receivedValue == expectedValue) {
		  	  		f_TEST_API_Subject_setverdict(pass, "Task: Received expected value!");
		  	  		p_taskPort.send(EV_TASK_FINISH);
		  	  	} else {
		  	  		f_TEST_API_Subject_setverdict(fail, "Task: Unexpected received value!");
		  	  		p_taskPort.send(EV_TASK_ERROR);
		  	  	}
		  	}
		}
	}
	
	type component
	CT_GPIO_TaskBuilder extends CT_TASK_BUILDER_TaskBuilder, CT_TEST_API_Subject { }
	
	function f_GPIO_TaskBuilder(CT_INTERFACE_STORE_InterfaceStore interfaceStore)
	runs on CT_GPIO_TaskBuilder {
	  connect(self:p_interfaceStore, interfaceStore:p_interfaceStoreClient);
	  
	  var RT_TASK_BUILDER_TaskBuilderMission receivedMission;
	  alt {
	    [] p_taskBuilderPort.receive(RT_TASK_BUILDER_TaskBuilderMission:{TaskName := ?, CreatedTask := omit})
	    -> value receivedMission {
	    	select(receivedMission.TaskName) {
	    	  case("receive_high") {
	    	    // get interface
	    	    var RT_INTERFACE_STORE_InterfaceElement receivedElement;
	    	    p_interfaceStore.send({Name:= "STORED DEVICE", Device:= omit , Controller:=omit});
	    	    p_interfaceStore.receive({Name:= "STORED DEVICE", Device:= ? , Controller:=omit}) -> value receivedElement;
	    	    
	    	    // start task
	    	    var CT_GPIO_ReceiveValueTask newTask := CT_GPIO_ReceiveValueTask.create;
	    	    newTask.start(f_GPIO_ReceiveValueTask(receivedElement.Device.UV_GPIO, HIGH));
	    	    
	    	    receivedMission.CreatedTask := newTask;
	    	    p_taskBuilderPort.send(receivedMission);
	    	  	break;
	    	  }
	    	  case("receive_low") {
	    	    // get interface
	    	    var RT_INTERFACE_STORE_InterfaceElement receivedElement;
	    	    p_interfaceStore.send({Name:= "STORED DEVICE", Device:= omit , Controller:=omit});
	    	    p_interfaceStore.receive({Name:= "STORED DEVICE", Device:= ? , Controller:=omit}) -> value receivedElement;
	    	    
	    	    // start task
	    	    var CT_GPIO_ReceiveValueTask newTask := CT_GPIO_ReceiveValueTask.create;
	    	    newTask.start(f_GPIO_ReceiveValueTask(receivedElement.Device.UV_GPIO, LOW));
	    	    receivedMission.CreatedTask := newTask;
	    	    p_taskBuilderPort.send(receivedMission);
	    	  	break;
	    	  }
	    	  case else {
	    	    f_TEST_API_Subject_setverdict(fail, "TaskBuilder: TaskName not a valid one!");
	    	    break; 
	    	  }
	    	}
	    	repeat;
	    }
	  }
	}
}
